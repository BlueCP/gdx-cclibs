buildscript {
  repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.2.3'
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
  }
}

apply plugin: 'io.codearte.nexus-staging'

ext {
  GROUPID = 'com.cyphercove.gdx'
  LIBRARY_VERSION = '1.1.0-SNAPSHOT'
}

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  ext {
    gdxVersion = '1.9.7-SNAPSHOT'
  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
  }

  group = GROUPID
  version = LIBRARY_VERSION
}

subprojects {
  eclipse.project.name = projectName
  project.group = rootProject.properties.GROUPID
}

configure([project(':covetools'), project(':flexbatch'), project(':gdxtokryo')])  {
  apply plugin: 'java'

  ext {
    wagonVersion = '2.10'
    junitVersion = '4.12'
  }

  dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    testCompile "junit:junit:$junitVersion"
  }

}

project(':covetools-android') {
  apply plugin: 'com.android.library'

  dependencies {
    compile project(':covetools')
    compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
  }
}

tasks.eclipse.doLast {
  delete ".project"
  delete ".classpath"
  delete ".settings/"
}
