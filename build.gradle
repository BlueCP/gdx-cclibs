buildscript {
  repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.2.0'
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
  }
}

ext {
  GROUPID = 'com.cyphercove.gdx'
  LIBRARY_VERSION = '1.1.0-SNAPSHOT'
}

apply plugin: 'distribution'
apply plugin: 'io.codearte.nexus-staging'

allprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  ext {
    gdxVersion = '1.9.7-SNAPSHOT'
  }

  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
  }

  group = GROUPID
  version = getLibraryVersion()
}

def isReleaseBuild() {
  return false
}

def getLibraryVersion() {
  if (isReleaseBuild()) {
    return LIBRARY_VERSION.replace("-SNAPSHOT", "")
  } else {
    return LIBRARY_VERSION;
  }
}

subprojects {
  eclipse.project.name = projectName
}

configure([project(':covetools'), project(':flexbatch'), project(':gdxtokryo')])  {
  apply plugin: 'java'

  ext {
    wagonVersion = '2.10'
    junitVersion = '4.12'
  }

  dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    testCompile "junit:junit:$junitVersion"
  }

  configurations {
    custom
    compile.extendsFrom custom
  }
}

project(':covetools-android') {
  configurations {
    custom
    compile.extendsFrom custom
  }

  dependencies {
    compile project(':covetools')
    compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
  }
}

nexusStaging {
  packageGroup = 'com.cyphercove.gdx'
  username = ossrhUsername
  password = ossrhPassword
}

tasks.eclipse.doLast {
  delete ".project"
  delete ".classpath"
  delete ".settings/"
}
